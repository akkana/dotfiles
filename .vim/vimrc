" Akkana's vimrc

syntax on

" In the 64-bit version of debian stretch, vim is grabbing the mouse
" and doing unpredictable things with it instead of just letting
" the terminal handle it.
" Turns out I don't need it, but root does. Put it here in case
" it becomes needed later.
" set mouse=

set background=light
" Enable 256-bit colorscheme
" set t_Co=256
colorscheme akk

" Try to move .viminfo out of my homedir.
" But it's ignored. And why is it += anyway?
set viminfo+=n~/.vim/viminfo

" Options for vimdiff:
if &diff
    " And set the window size: Xdefaults is ignored for gvim.
    set lines=58
    set columns=180

    " When resizing, keep the separator in the middle of the window:
    autocmd VimResized * exec "normal \<C-w>="

    " gvim's default color schemes are completely unreadable.
    " Here's how to specify a particular scheme (but they're all bad):
    " colorscheme some_other_scheme
    " http://stackoverflow.com/questions/2019281/load-different-colorscheme-when-using-vimdiff

    " And here's how to clean up and highlight the diff sections:
    highlight DiffAdd    cterm=bold gui=none guibg=Yellow
    highlight DiffDelete cterm=bold gui=none guibg=Yellow
    highlight DiffChange cterm=bold gui=none guibg=Yellow
    highlight DiffText   cterm=bold gui=none guibg=Yellow
    " To remove syntax highlighting inside diff blocks,
    " add guifg=Black to each of those highlight lines.
    " The original also had ctermfg=10 ctermbg=17 in each line
    " after the cterm bold.

    nmap du :wincmd w<cr>:normal u<cr>:wincmd w<cr>
endif

" disable the evil alt screen
" set t_ti= t_te=

" Reduce the delay on inserting with O.
" Another possibility is to turn off esckeys.
" http://stackoverflow.com/questions/2158516/vim-delay-before-o-opens-a-new-line
set ttimeoutlen=100

" But really, a better solution is to map something to open up space
" in an email message so I don't always have to do o <ret> <ESC> O (wait)
map <leader>o o<CR><ESC>O

" allow backspacing between lines
set bs=2

" Use one space, not two, after a period
set nojoinspaces

" Fix for a security bug, see http://www.guninski.com/vim1.html
"set modelines=0
" Yikes -- that turns off being able to use modelines at all! Not worth it.
set modeline
" Supposedly this prevents executing arbitrary commands inside modelines
" in files I don't own.
set secure

" Highlight trailing whitespace.
" set list shows it, set nolist un-shows it.
" http://vim.wikia.com/wiki/Highlight_unwanted_spaces
" set listchars=tab:>-,trail:-
set listchars=trail:Â¬

" Another way to highlight trailing whitespace.
" You can also define a separate color, Whitespace (see colors/akk.vim)
" but you still only have the basic 8 colors to work with.
" match Whitespace '\s\+$'

" Line and paragraph wrapping
map <leader>l r<CR>J
map <leader>p r<CR>gq}

" I too often parenthesize things then change my mind
map <leader>, 2s, <ESC>

" Split one run-on sentence into two:
map <leader>c 2s.  <ESC> ~

" De-elainify email, from people who don't mark quotes or wrap lines.
map <leader>e :.,$!deelainify<CR>

" Google the current line
"map <leader>g 0"xy$:!lynx -dump -accept_all_cookies 'www.google.com/search?q=^Rx' \| grep ^Rx \| grep -v http \| head -n 20 \| grep ^Rx

map <leader>g 0"xy$:!lynx -dump -accept_all_cookies 'www.google.com/search?q=x'

" ctrl-space quits insert mode and does bad things.
" Stop that!
" One way of testing: :^K^space:
" Other possible ways of saying it: <Nul>, <Char-0>, <C- >, <C-@>
" p0g on #vim finally found the answer: imap is the important one.
imap <Nul> <Space>
map <Nul> <Nop>
vmap <Nul> <Nop>
cmap <Nul> <Nop>
nmap <Nul> <Nop>

" From the vim list: tag-ify the previous word
inoremap <leader>, <esc>diwi<<esc>pa><cr></<esc>pa><esc>kA

" Case fold searches by default, unless they contain some capital letters.
set ignorecase
set smartcase

" Set wrapping/textwidth according to file location and type.
function! SetupEnvironment()
  let l:path = expand('%:p')
  if (l:path =~ '/home/akkana/Procmail')
    " When editing spam filters or web databases, disable wrapping:
    setlocal textwidth=0
  elseif (&ft == 'python' || &ft == 'c' || &ft == 'html' || &ft == 'php')
    setlocal textwidth=0
  " File extensions vim should know about, but doesn't
  " unless you download special filetype extensions:
  elseif (l:path =~ '.csv')
  elseif (&ft == 'mail')
    " Slightly narrower width for mail (and override mutt's override):
    setlocal textwidth=68
  else
    " default textwidth slightly narrower than the default
    setlocal textwidth=70
  endif
endfunction
autocmd! BufReadPost,BufNewFile * call SetupEnvironment()

" Indent specifically for the current filetype
filetype indent on
" Set indent level to 4, using spaces, not tabs
set expandtab shiftwidth=4

" Stuff for mail
" autocmd FileType mail source ~/.vimmailrc
" Mutt overrides textwidth for mail, unless I override it again:
" autocmd FileType mail set textwidth=68

" Turn off the 'thanks for flying vim' messages
set notitle

" http://software.newsforge.com/software/06/06/08/1431244.shtml?tid=130&tid=138
"set hls
function ToggleHLSearch()
  if &hls
    set nohls
  else
    set hls
  endif
endfunction
nmap <silent> <C-S-n> <ESC>:call ToggleHLSearch()<CR>

" Also from nomis:
set pastetoggle=<F11>

" Turn on spellchecking
setlocal spell spelllang=en_us

" From Juliet Kemp's vimscript article
fu! WordsThisLine() range
  let words = 0
  for linenum in range(a:firstline, a:lastline)
    let words += len(split(getline(linenum)))
  endfor
  echo words
  return words
endfu

fu! Dictionary()
  let dict = "dict"
  let wordUnderCursor = expand("<cword>")
  let command = "!" . dict . " " . wordUnderCursor
  execute command
endfu

fu! Label() range
  call inputsave()
  let label = input("Enter label: ")
  call inputrestore()
  for linenum in range(a:firstline, a:lastline)
    let curline = getline(linenum)
    call setline(linenum, label . " " . curline)
  endfor
endfu

" Thinkpad has F1 where ESC should be, so remap it:
" (but that shouldn't be necessary if we remap it at system level in rc.local)
" (but loadkeys no longer works in Ubuntu Pangolin, sigh ...)
map <F1> <Esc>
imap <F1> <Esc>

" Use GIMP style for GIMP files -- see gimp/devel-docs/c.vim
autocmd BufNewFile,BufRead /home/akkana/outsrc/gimp*/*.[ch] source /home/akkana/outsrc/gimp/devel-docs/c.vim
